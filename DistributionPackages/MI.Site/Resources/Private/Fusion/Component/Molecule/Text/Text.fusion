prototype(MI.Site:Component.Molecule.Text) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        leadContent = ${PropTypes.string}
        titleTagName = ${PropTypes.string.isRequired}
        titleFontSize = ${PropTypes.string.isRequired}
        titleContent = ${PropTypes.string}
        textFontSize = ${PropTypes.string.isRequired}
        textColumns = ${PropTypes.string}
        textContent = ${PropTypes.string}
        gridArea = ${PropTypes.string}
        extraClass = ${PropTypes.string}
    }

    renderer = afx`
        <div
            class.main="text"
            class.gridArea={props.gridArea ? props.gridArea : "[grid-area:medium-inner]"}
            class.extra={props.extraClass}
        >
            <MI.Site:Component.Atom.Lead
                @if={props.leadContent && props.titleContent}
                extraClass="mb-2.5"
            >
                {props.leadContent}
            </MI.Site:Component.Atom.Lead>

            <MI.Site:Component.Atom.Headline
                @if={props.titleContent}
                tagName={props.titleTagName}
                fontSize={props.titleFontSize}
                extraClass="mb-5"
            >
                {props.titleContent}
            </MI.Site:Component.Atom.Headline>

            <MI.Site:Component.Atom.Paragraph
                fontSize={props.textFontSize}
                columns={props.textColumns}
            >
                {props.textContent}
            </MI.Site:Component.Atom.Paragraph>
        </div>
    `
}
