prototype(MI.Site:Component.Atom.AccordionItem) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        id = ${PropTypes.anyOf(PropTypes.string, PropTypes.integer).isRequired}
        title = ${PropTypes.string.isRequired}
        content = ${PropTypes.string}
    }

    renderer = afx`
        <div
            x-data={"MI_Site_Component_Atom_AccordionItem('" + props.id + "')"}
            class="my-2.5 last:mb-0 first:mt-0"
        >
            <button
                x-on:click="toggle"
                class="w-full px-5 md:px-10 py-3 md:py-5 transition-[background,color] duration-500 rounded-xl text-secondary-700 text-left hover:bg-primary-50"
                x-bind:class="open && 'text-primary bg-primary-50'"
            >
                <span class="w-full block text-lg font-bold">
                    {props.title}
                </span>
            </button>
            <div
                class="mt-2.5"
                x-show="open"
                x-collapse
            >
                <div class="w-full rounded-xl border-[1px] border-secondary-100 p-5 md:p-10">
                    {props.content}
                </div>
            </div>
        </div>
    `
}
