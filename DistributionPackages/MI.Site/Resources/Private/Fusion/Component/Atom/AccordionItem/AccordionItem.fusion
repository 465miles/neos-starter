prototype(MI.Site:Component.Atom.AccordionItem) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        id = ${PropTypes.anyOf(PropTypes.string, PropTypes.integer).isRequired}
        title = ${PropTypes.string.isRequired}
        content = ${PropTypes.string}
    }

    renderer = afx`
        <div class="border-b border-gray-200">
            <button
                class="w-full px-5 md:px-10 py-3 md:py-5 text-lg text-left font-bold transition-color duration-500"
                x-on:click={"selected !== '" + props.id + "' ? selected = '" + props.id + "' : selected = null"}
                x-bind:class={"selected == '" + props.id + "' ? 'text-blue-500 border-b border-gray-100' : ''"}
            >
                <MI.Site:Component.Atom.Headline
                    tagName='h3'
                    fontSize='4'
                    color='text-inherit'
                >
                    {props.title}
                </MI.Site:Component.Atom.Headline>
            </button>
            <div
                x-show={"selected == '" + props.id + "'"}
                x-collapse
                x-bind:class={"selected == '" + props.id + "' ? 'my-10' : 'my-0'"}
                class="overflow-hidden px-5 md:px-10 transition-[margin] duration-500 my-0"
            >
                {props.content}
            </div>
        </div>
    `
}
