prototype(465M.Site:Component.Molecule.Media) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        mediaType = ${PropTypes.string.isRequired}
        imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\EelHelpers\\ImageSourceHelperInterface')}
        videoUri = ${PropTypes.string}
        videoId = ${PropTypes.string}
        caption = ${PropTypes.string}
        spacingTop = ${PropTypes.string}
        spacingBottom = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        media = Neos.Fusion:Match {
            @subject = ${props.mediaType}

            @default = afx`
                <p class="text-lg text-red-500">Noch kein Medientyp gew√§hlt.</p>
            `

            image = afx`
                <465M.Site:Component.Atom.Image
                    imageSource={props.imageSource}
                />
            `

            video = afx`
                <465M.Site:Component.Atom.Video
                    videoPoster={props.imageSource}
                    videoUri={props.videoUri}
                />
            `

            video-youtube = afx`
                <465M.Site:Component.Atom.VideoExternal
                    provider="youtube"
                    videoId={props.videoId}
                />
            `

            video-vimeo = afx`
                <465M.Site:Component.Atom.VideoExternal
                    provider="vimeo"
                    videoId={props.videoId}
                />
            `
        }

        renderer = afx`
            <465M.Site:Component.Organism.Container
                spacingTop={props.spacingTop}
                spacingBottom={props.spacingBottom}
            >
                <figure class.main="w-full flex flex-col gap-6">
                    {props.media}
                    <465M.Site:Component.Atom.MediaCaption content={props.caption} @if.has={props.caption} />
                </figure>
            </465M.Site:Component.Organism.Container>
        `
    }
}
