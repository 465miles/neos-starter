prototype(465M.Site:Component.Atom.Headline) < prototype(Neos.Fusion:Component) {

    @propTypes {
        tagName = ${PropTypes.string.isRequired}
        fontSize = ${PropTypes.string.isRequired}
        spacingTop = ${PropTypes.string}
        spacingBottom = ${PropTypes.string}
        color = ${PropTypes.string}
        content = ${PropTypes.string.isRequired}
        extraClass = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        fontSizeClass = Neos.Fusion:Match {
            @subject = ${props.fontSize}
            @default = 'xl'
            "base" = "text-xl"
            "sm" = "text-lg"
            "lg" = "text-2xl"
            "xl" = "text-3xl"
            "2xl" = "text-4xl"
            "3xl" = "text-5xl"
            "4xl" = "text-6xl"
            "5xl" = "text-7xl"
        }

        spacingTopClass = Neos.Fusion:Match {
            @subject = ${props.spacingTop}
            @default = 'none'
            "none" = "mt-0"
            "xs" = "mt-8"
            "sm" = "mt-12"
            "md" = "mt-16"
            "lg" = "mt-24"
            "xl" = "mt-36"
            "2xl" = "mt-48"
        }

        spacingBottomClass = Neos.Fusion:Match {
            @subject = ${props.spacingBottom}
            @default = 'none'
            "none" = "mb-0"
            "xs" = "mb-8"
            "sm" = "mb-12"
            "md" = "mb-16"
            "lg" = "mb-24"
            "xl" = "mb-36"
            "2xl" = "mb-48"
        }

        renderer = afx`
            <div
                class.main="headline"
                class.prose='prose'
                class.width="w-full"
                class.color={props.color ? props.color : "text-gray-700"}
                class.spacingTop={props.spacingTopClass}
                class.spacingBottom={props.spacingBottomClass}
                class.extra={props.extraClass}>
                <Neos.Fusion:Tag
                    tagName={props.tagName}
                    attributes.class.fontSize={props.fontSizeClass}>
                    {props.content}
                </Neos.Fusion:Tag>
            </div>
        `
    }
}
