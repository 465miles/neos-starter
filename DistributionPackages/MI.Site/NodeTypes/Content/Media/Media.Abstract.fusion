prototype(MI.Site:Content.Media.Abstract) < prototype(Neos.Neos:ContentComponent) {

    mediaType = ${q(node).property('mediaType')}
    imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
        asset = ${q(node).property('image')}
    }
    videoUri = Neos.Fusion:ResourceUri {
        resource = ${q(node).property('video').resource}
    }
    videoId = ${q(node).property('videoId')}

    showMeta = ${q(node).property('showMeta')}

    showTitle = ${q(node).property('showTitle')}
    alternativeTitle = ${String.nl2br(q(node).property('alternativeTitle'))}

    showCaption = ${q(node).property('showCaption')}
    alternativeCaption = ${String.nl2br(q(node).property('alternativeCaption'))}

    showCopyright = ${q(node).property('showCopyright')}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        mediaTitle = Neos.Fusion:Case {
            image {
                condition = ${props.mediaType == 'image'}
                renderer = ${q(node).property('image').title}
            }
            video {
                condition = ${props.mediaType == 'video'}
                renderer = ${q(node).property('video').title}
            }
        }

        mediaCaption = Neos.Fusion:Case {
            image {
                condition = ${props.mediaType == 'image'}
                renderer = ${q(node).property('image').caption}
            }
            video {
                condition = ${props.mediaType == 'video'}
                renderer = ${q(node).property('video').caption}
            }
        }

        mediaCopyright = Neos.Fusion:Case {
            image {
                condition = ${props.mediaType == 'image'}
                renderer = ${q(node).property('image').copyrightNotice}
            }
            video {
                condition = ${props.mediaType == 'video'}
                renderer = ${q(node).property('video').copyrightNotice}
            }
        }

        renderer = afx`
            <MI.Site:Component.Molecule.Media {...props} />
        `
    }
}
