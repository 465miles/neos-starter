prototype(MI.Site:Component.Atom.AccordionItem) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        id = ${PropTypes.anyOf(PropTypes.string, PropTypes.integer).isRequired}
        title = ${PropTypes.string.isRequired}
        content = ${PropTypes.string}
    }

    renderer = afx`
        <div
            x-data={"MI_Site_Component_Atom_AccordionItem('" + props.id + "')"}
            class="transition-[border]"
            x-bind:class="open && 'border-b border-gray-200'"
        >
            <button
                x-on:click="toggle"
                class="w-full px-5 md:px-10 py-3 md:py-5 text-lg text-left font-bold transition-color border-b border-gray-100 duration-500"
                x-bind:class="open && 'text-blue-500'"
            >
                <MI.Site:Component.Atom.Headline
                    tagName='h3'
                    fontSize='4'
                    color='text-inherit'
                >
                    {props.title}
                </MI.Site:Component.Atom.Headline>
            </button>
            <div
                x-show="open"
                x-collapse
            >
                <div class="w-full px-5 md:px-10 py-10">
                    {props.content}
                </div>
            </div>
        </div>
    `
}
