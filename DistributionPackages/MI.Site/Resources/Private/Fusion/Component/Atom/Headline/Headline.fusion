prototype(MI.Site:Component.Atom.Headline) < prototype(Neos.Fusion:Component) {

    @propTypes {
        @strict = true
        tagName = ${PropTypes.string.isRequired}
        fontSize = ${PropTypes.oneOf([
            'base', 'sm', 'lg', 'xl', '2xl', '3xl', '4xl', '5xl'
        ])}
        color = ${PropTypes.string}
        gridArea = ${PropTypes.string}
        content = ${PropTypes.string}
        extraClass = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        mappedFontSize = Neos.Fusion:Match {
            @subject = ${props.fontSize}
            @default = ${this.base}
            base = 'text-xl'
            sm = 'text-lg'
            lg = 'text-2xl'
            xl = 'text-3xl'
            2xl = 'text-4xl'
            3xl = 'text-5xl'
            4xl = 'text-6xl'
            5xl = 'text-7xl'
        }

        renderer = afx`
            <div
                class.main="headline prose w-full"
                class.color={props.color ? props.color : "text-gray-700"}
                class.gridArea={props.gridArea ? props.gridArea : "[grid-area:small-inner]"}
                class.extra={props.extraClass}
            >
                <Neos.Fusion:Tag
                    tagName={props.tagName}
                    attributes.class={props.mappedFontSize}
                >
                    {props.content}
                </Neos.Fusion:Tag>
            </div>
        `
    }
}
